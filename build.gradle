subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group 'net.duvdev'
    version '1.1.0'

    sourceCompatibility = 1.8

    task sourcesJar(type: Jar, dependsOn: classes) {
      classifier = 'sources'
      from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: classes) {
      classifier = 'javadoc'
      from javadoc.destinationDir
    }

    publishing {
      publications {
        mavenJava(MavenPublication) {
          from components.java
          artifact sourcesJar
          artifact javadocJar
        }
      }
      repositories {
        mavenLocal()
      }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    test {
        include 'net/duvdev/rsqueezea/**'
    }
}

project(':rsqueezea-common') {
}

project(':rsqueezea-cli') {
    dependencies {
        compile project(':rsqueezea-common')

        compile 'com.google.code.findbugs:jsr305:3.0.2'

        compile 'commons-io:commons-io:2.6'

        compile 'com.beust:jcommander:1.72'

        compile 'org.bouncycastle:bcprov-jdk15on:1.59'
        compile 'org.bouncycastle:bcpkix-jdk15on:1.59'

        compile 'com.google.zxing:javase:3.3.3'
    }

    task executableJar(type: Jar) {
        baseName = 'rsqueezea-cli-exe'

        manifest {
            attributes 'Main-Class': 'net.duvdev.rsqueezea.cli.Main'
        }

        from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }

        from('../') {
            into 'META-INF'
            include 'LICENSE'
            rename 'LICENSE', 'rsqueezea-LICENSE'
        }

        with jar
    }
}
